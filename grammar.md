PROGRAM ::= STATEMENT
PROGRAM ::= FUNCLIST

FUNCLIST ::= FUNCDEF FUNCLIST
FUNCLIST ::= ''

FUNCDEF ::= def TYPE func_ident ( PARAMLIST ) { STATELIST }

PARAMLIST ::= TYPE ident OPTINAL_PARAMLIST 
PARAMLIST ::= ''

OPTINAL_PARAMLIST ::= , TYPE ident OPTINAL_PARAMLIST
OPTINAL_PARAMLIST ::= ''

TYPE ::= int 
TYPE ::= float
TYPE ::= string
TYPE ::= void

STATEMENT ::= VARDECL ;
STATEMENT ::= ATRIBSTAT ;
STATEMENT ::= RETURNSTAT ;
STATEMENT ::= IFSTAT ;
STATEMENT ::= FORSTAT ;
STATEMENT ::= { STATELIST }
STATEMENT ::= ;

VARDECL ::= TYPE ident INT_CONST 

INT_CONST ::= [ int_constant ] INT_CONST
INT_CONST ::= ''

ATRIBSTAT ::= LVALUE = STAT

STAT ::= EXPRESSION
STAT ::= FUNCCALL

FUNCCALL ::= func_ident ( PARAMLISTCALL )

PARAMLISTCALL ::= ident OPTINAL_PARAMLISTCALL 
PARAMLISTCALL ::= ''

OPTINAL_PARAMLISTCALL ::= , ident OPTINAL_PARAMLISTCALL
OPTINAL_PARAMLISTCALL ::= ''

RETURNSTAT ::= return OPTIONAL_IDENT 
OPTIONAL_IDENT ::= ident
OPTIONAL_IDENT ::= ''


IFSTAT ::= if ( EXPRESSION ) STATEMENT OPTIONAL_ELSE
OPTIONAL_ELSE ::= else STATEMENT
OPTIONAL_ELSE ::= ''

FORSTAT ::= for ( ATRIBSTAT ; EXPRESSION ; ATRIBSTAT ) STATEMENT

STATELIST ::= STATEMENT OPTIONAL_STATELIST
OPTIONAL_STATELIST ::= STATELIST
OPTIONAL_STATELIST ::= ''

EXPRESSION ::= NUMEXPRESSION OPTIONAL_COMPARATOR
EXPRESSION ::= not EXPRESSION

OPTIONAL_COMPARATOR ::= COMPARATOR NUMEXPRESSION
OPTIONAL_COMPARATOR ::= LEAST_PRECEDENCE_COMP NUMEXPRESSION
OPTIONAL_COMPARATOR ::= ''

LEAST_PRECEDENCE_COMP ::= or

COMPARATOR ::= < 
COMPARATOR ::= > 
COMPARATOR ::= <= 
COMPARATOR ::= >=
COMPARATOR ::= ==
COMPARATOR ::= !=
COMPARATOR ::= and

NUMEXPRESSION ::= TERM NUMEXPRESSIONAUX

NUMEXPRESSIONAUX ::= OPERATOR TERM NUMEXPRESSIONAUX
NUMEXPRESSIONAUX ::= ''

OPERATOR ::= +
OPERATOR ::= -

TERM ::= UNARYEXPR TERM_AUX
TERM_AUX ::= HIGH_PRECEDENCE_OPERATOR UNARYEXPR TERM_AUX
TERM_AUX ::= ''
 
HIGH_PRECEDENCE_OPERATOR ::= %
HIGH_PRECEDENCE_OPERATOR ::= /
HIGH_PRECEDENCE_OPERATOR ::= *

UNARYEXPR ::= OPERATOR FACTOR
UNARYEXPR ::= FACTOR

FACTOR ::= int_constant
FACTOR ::= float_constant
FACTOR ::= string_constant
FACTOR ::= null
FACTOR ::= LVALUE
FACTOR ::= ( NUMEXPRESSION )

LVALUE ::= ident LVALUEAUX
LVALUEAUX ::= [ NUMEXPRESSION ] LVALUEAUX
LVALUEAUX ::= ''